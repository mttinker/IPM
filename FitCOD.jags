# Model to fit cause-specific mortality using proportional hazards approach, 
# using observed data of necropsy analyses of stranded carcasses and annual
# estimates of age/sex specific survival by area and year, 1998-2012
#
model{
# OBSERVED VARIABLES
# Observed node: case frequency distributions by cause of death
for (i in 1:Ncasedist){		
	# Observed node: multinomial distributions of cases
	Casedist[i,1:Ncod] ~ dmulti(delta[1:Ncod,Cstage[i],Carea[i],Cyear[i],Cqtr[i]],ncase[i])
}
# Observed node: survival rates by stage, area and year
for (i in 1:NSrates){
	Slg[i] ~ dnorm(logit(S[Sstage[i],Sarea[i],Syear[i]]),tauS[i])
}
# LATENT VARIABLES
# Loop through Areas, Years, Quarters and COD to compute cause specific hazards (Year 1 = 1990)
for (a in 1:Nareas){
	for (y in 1:Nyrs){
		for (i in 1:Nstage){
			for (q in 1:Nqtr){	
				for (x in 1:Ncod){		
					gam[x,i,a,y,q] <- exp(B[x,i] + phi[x]*pK[a,y])*exp(Z[x,a,y]+Q[x,q]) 
					h[x,i,a,y,q] <- exp(gam[x,i,a,y,q]*Stagefilter[x,i] + gam0)
					delta[x,i,a,y,q] <- (h[x,i,a,y,q] - h0)/(UCH[i,a,y,q] - Ncod*h0)
				}
				UCH[i,a,y,q] <- sum(h[1:Ncod,i,a,y,q])
			}
			S[i,a,y] <- exp(-1*sum(UCH[i,a,y,1:Nqtr]))
		}
	}
}
#
# FIT PRIORS 
# (using vague cauchy or half cauchy priors for most params, as per Gelman 2008)
# Priors for B and phi params, Z and Q matrices, and variance params: 
for (x in 1:Ncod){
	phi[x] ~ dt(0, 1/2.5^2 ,1)   # density-dependent effect, allows hazard to increase or decrease with ppn K
	for (i in 1:Nstage){
		Beta[x,i] ~ dt(0, 1/2.5^2 ,1) T(-10,10)# Magnitude of effect of hazard x on stage i (log of log hazards)		
		B[x,i] <- Beta[x,i]*Stagefilter[x,i]
	}
	for (a in 1:Nareas){
		Zmn[x,a] ~ dnorm(0,tauA)
		for (y in 1:Nyrs){
			# Z random effect, describes var of relative magnitude of hazard over space and time
			Z[x,a,y] ~ dnorm(Zmn[x,a],tauT)
		}
	}
	for (q in 1:Nqtr){
		Q[x,q] ~ dnorm(0,tauQ)
	}
}
sigQ ~ dt(0, 1/1^2 ,1) T(0,) # variance in relative frequency of COD by season
tauQ <- pow(sigQ,-2)
sigA ~ dt(0, 1/1^2 ,1) T(0,) # variance in relative frequency of COD by area
tauA <- pow(sigA,-2)
sigT ~ dt(0, 1/1^2 ,1) T(0,) # variance in relative frequency of COD by season
tauT <- pow(sigT,-2)
#
# Some nuiscense params
h0 <- 0.0005
gam0 <- log(h0)
}