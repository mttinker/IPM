# Model to fit cause-specific mortality using a competing hazards formulation. 
# Observed data sets: necropsy analyses of ~ 600 stranded carcasses, and annual
# estimates of age/sex-specific survival by area and year, 1998-2012
#
model{
# OBSERVED VARIABLES
# Observed node: frequency distributions of carcasses by cause of death
for (i in 1:Ncasedist){		
	# Distributions of cases by COD as multinomial random variable
	Casedist[i,1:Ncod] ~ dmulti(delta[1:Ncod,Cstage[i],Carea[i],Cyear[i],Cqtr[i]],ncase[i])
}
# Observed node: survival rates by stage, area and year
for (i in 1:NSrates){
	# logit-transformed survival rate as normal random variable
	Slg[i] ~ dnorm(logit(S[Sstage[i],Sarea[i],Syear[i]]),tauS[i])
}
# LATENT VARIABLES
# Loop through Areas, Years, Quarters and COD to compute cause specific hazards (Year 1 = 1990)
for (a in 1:Nareas){
	for (y in 1:Nyrs){
		for (i in 1:Nstage){
			for (q in 1:Nqtr){	
				for (x in 1:Ncod){		
					gam[x,i,a,y,q] <- exp(B[x,i] + phi[x]*pK[a,y] + Z[x,a,y]+Q[x,q])
					h[x,i,a,y,q] <- exp(gam[x,i,a,y,q]*Stagefilter[x,i] + gam0)
					delta[x,i,a,y,q] <- (h[x,i,a,y,q] - h0)/(UCH[i,a,y,q] - Ncod*h0)
				}
				UCH[i,a,y,q] <- sum(h[1:Ncod,i,a,y,q])
			}
			S[i,a,y] <- exp(-1*sum(UCH[i,a,y,1:Nqtr]))
		}
	}
}
#
# FIT PRIORS 
# (using vague cauchy or half cauchy priors for most params, as per Gelman 2008)
# Priors for B and phi params, Z and Q random effect matrices, and variance params: 
for (x in 1:Ncod){
	phi[x] ~ dt(0, 1/2.5^2 ,1)   # density-dependent effect, allows hazard to increase or decrease with ppn K
	for (i in 1:Nstage){
		Beta[x,i] ~ dt(0, 1/2.5^2 ,1) T(-10,10)# Magnitude of hazard x on stage i (log of log hazards)		
		# Notes: log of log haz used to ensure realized hazard ratio of a given COD >=1
		#  "Stagefilter" matrix foreces End Laction hazard to 0 for stages other than reproductive females
		B[x,i] <- Beta[x,i]*Stagefilter[x,i]
	}
	for (a in 1:Nareas){
		Zmn[x,a] ~ dnorm(0,tauA)
		for (y in 1:Nyrs){
			# Z = random effect matrix, variation in relative magnitude of hazard over space and time
			Z[x,a,y] ~ dnorm(Zmn[x,a],tauT)
		}
	}
	for (q in 1:Nqtr){
		# Q = random effect matrix, variation in relative magnitude of hazard by season
		Q[x,q] ~ dnorm(0,tauQ)
	}
}
sigQ ~ dt(0, 1/1^2 ,1) T(0,) # variance in relative frequency of COD by season
tauQ <- pow(sigQ,-2)
sigA ~ dt(0, 1/1^2 ,1) T(0,) # variance in relative frequency of COD by area
tauA <- pow(sigA,-2)
sigT ~ dt(0, 1/1^2 ,1) T(0,) # variance in relative frequency of COD by season
tauT <- pow(sigT,-2)
#
# Some nuiscense params: baseline hazard rate (very low, 99% survival without any other COD)
h0 <- 0.0005
gam0 <- log(h0)
}